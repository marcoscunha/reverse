divert(-1)
define(NEWPROC,) dnl

define(BARRIER, `{
	unsigned long	Error, Cycle;
	long		Cancel, Temp;
 
    DCACHE_INVAL(&($1).mutex, sizeof(pthread_mutex_t));
	Error = pthread_mutex_lock(&($1).mutex);
	if (Error != 0) {
		printf("Error while trying to get lock in barrier.\n");
		exit(-1);
	}
    DCACHE_INVAL(&($1).cycle, sizeof(unsigned long));
	Cycle = ($1).cycle;
    DCACHE_INVAL(&($1).counter, sizeof(unsigned long));
	if (++($1).counter != ($2)) {
        DCACHE_FLUSH(&($1).counter, sizeof(unsigned long));
		pthread_setcancelstate(PTHREAD_CANCEL_DISABLE, (int *)&Cancel);
        DCACHE_INVAL(&($1).cycle, sizeof(unsigned long));
		while (Cycle == ($1).cycle) {
            DCACHE_INVAL(&($1).cv, sizeof(pthread_cond_t));
			Error = pthread_cond_wait(&($1).cv, &($1).mutex);
			if (Error != 0) {
				break;
			}
            DCACHE_INVAL(&($1).cycle, sizeof(unsigned long));
		}
		pthread_setcancelstate(Cancel, (int *)&Temp);
	} else {
		($1).cycle = !($1).cycle;
        DCACHE_FLUSH(&($1).cycle, sizeof(unsigned long));
		($1).counter = 0;
        DCACHE_FLUSH(&($1).counter, sizeof(unsigned long));

        DCACHE_INVAL(&($1).cv, sizeof(pthread_cond_t)); 
		Error = pthread_cond_broadcast(&($1).cv);
	}
	pthread_mutex_unlock(&($1).mutex);
}')

define(BARDEC, `
struct {
    #ifdef WRITEBACK
	pthread_mutex_t	mutex __attribute__((aligned(32)));
    int32_t spare1[LOCK_SPARE_SIZE];
    #else
	pthread_mutex_t	mutex;
    #endif
	pthread_cond_t	cv;
    int32_t spare2[7];
	unsigned long	counter;
    int32_t spare3[7];
	unsigned long	cycle;
    int32_t spare4[7];

} ($1);
')

define(BARINIT, `{
	unsigned long	Error;

	Error = pthread_mutex_init(&($1).mutex, NULL);
	if (Error != 0) {
		printf("Error while initializing barrier.\n");
		exit(-1);
	}

	Error = pthread_cond_init(&($1).cv, NULL);
	if (Error != 0) {
		printf("Error while initializing barrier.\n");
		pthread_mutex_destroy(&($1).mutex);
		exit(-1);
	}

	($1).counter = 0;
	($1).cycle = 0;
}')

define(BAREXCLUDE, `{;}')

define(BARINCLUDE, `{;}')

define(GSDEC, `long ($1);')
define(GSINIT, `{ ($1) = 0; }')
define(GETSUB, `{
  if (($1)<=($3))
    ($2) = ($1)++;
  else {
    ($2) = -1;
    ($1) = 0;
  }
}')

define(NU_GSDEC, `long ($1);')
define(NU_GSINIT, `{ ($1) = 0; }')
define(NU_GETSUB, `GETSUB($1,$2,$3,$4)')

define(ADEC, `long ($1);')
define(AINIT, `{;}')
define(PROBEND, `{;}')

define(LOCKDEC, 
`
#ifdef WRITEBACK
pthread_mutex_t ($1)__attribute__((aligned(32)));
#else
pthread_mutex_t ($1);
#endif

')
define(LOCKINIT, `{pthread_mutex_init(&($1), NULL);}')
define(LOCK, `{pthread_mutex_lock(&($1));}')
define(UNLOCK, `{pthread_mutex_unlock(&($1));}')

define(NLOCKDEC, `long ($1);')
define(NLOCKINIT, `{;}')
define(NLOCK, `{;}')
define(NUNLOCK, `{;}')

define(ALOCKDEC, `pthread_mutex_t $1[$2];')
define(ALOCKINIT, `{
	unsigned long	i, Error;

	for (i = 0; i < $2; i++) {
		Error = pthread_mutex_init(&$1[i], NULL);
		if (Error != 0) {
			printf("Error while initializing array of locks.\n");
			exit(-1);
		}
	}
}')
define(ALOCK, `{pthread_mutex_lock(&$1[$2]);}')
define(AULOCK, `{pthread_mutex_unlock(&$1[$2]);}')

define(PAUSEDEC, `
struct {
	pthread_mutex_t	Mutex;
	pthread_cond_t	CondVar;
	unsigned long	Flag;
} $1;
')
define(PAUSEINIT, `{
	pthread_mutex_init(&$1.Mutex, NULL);
	pthread_cond_init(&$1.CondVar, NULL);
	$1.Flag = 0;
}
')
define(CLEARPAUSE, `{
	$1.Flag = 0;
	pthread_mutex_unlock(&$1.Mutex);}
')
define(SETPAUSE, `{
	pthread_mutex_lock(&$1.Mutex);
	$1.Flag = 1;
	pthread_cond_broadcast(&$1.CondVar);
	pthread_mutex_unlock(&$1.Mutex);}
')
define(EVENT, `{;}')
define(WAITPAUSE, `{
	pthread_mutex_lock(&$1.Mutex);
	if ($1.Flag == 0) {
		pthread_cond_wait(&$1.CondVar, &$1.Mutex);
	}
}')
define(PAUSE, `{;}')

define(AUG_ON, ` ')
define(AUG_OFF, ` ')
define(TRACE_ON, ` ')
define(TRACE_OFF, ` ')
define(REF_TRACE_ON, ` ')
define(REF_TRACE_OFF, ` ')
define(DYN_TRACE_ON, `;')
define(DYN_TRACE_OFF, `;')
define(DYN_REF_TRACE_ON, `;')
define(DYN_REF_TRACE_OFF, `;')
define(DYN_SIM_ON, `;')
define(DYN_SIM_OFF, `;')
define(DYN_SCHED_ON, `;')
define(DYN_SCHED_OFF, `;')
define(AUG_SET_LOLIMIT, `;')
define(AUG_SET_HILIMIT, `;')

define(MENTER, `{;}')
define(DELAY, `{;}')
define(CONTINUE, `{;}')
define(MEXIT, `{;}')
define(MONINIT, `{;}')

define(WAIT_FOR_END, `{
	unsigned long	i, Error;
	for(i=0;i<pthread_count;i++)
	{
	Error = pthread_join(pthread_array[i],NULL);
	if (Error != 0) {
	printf("Error in pthread_join().\n");
	exit(-1);
	}
	}
}')

define(CREATE, `{
		long i, Error;
			for (i = 1; i < ($2); i++)
			{
#ifdef DISTRIBUTED
			pthread_attr_init(&(attr[i]));	
			if (CPU_MP_ID()==i)
			{
				assert(false);
			}
			else
			{
				attr[i].procid = i;
			}
			pthread_create(&(pthread_array[pthread_count++]),&(attr[i]),(void * (*)(void *))($1),NULL); 
#else
			pthread_create(&(pthread_array[pthread_count++]),NULL,(void * (*)(void *))($1),NULL); 
#endif
			}
		$1();
		}
')

define(MAIN_INITENV, `
pthread_array = (pthread_t*)malloc(sizeof(pthread_t)*($2));
#ifdef DISTRIBUTED
attr = (pthread_attr_t*)malloc(sizeof(pthread_attr_t)*($2));
#endif
')

define(MAIN_END, 
`printf("************* END OF MAIN ****************\n");
unsigned volatile a = 0;
while(a!= 1000) a++;
EXIT_SIM_SUCCESS;
*(volatile int*)0x0=0;')

define(MAIN_ENV,`
#include <pthread.h>
#include <sys/time.h>
#include <unistd.h>
#include <assert.h>
#include <stdlib.h>
#include <malloc.h>
static pthread_t *pthread_array; // tima macro
#ifdef DISTRIBUTED
pthread_attr_t *attr; // thread attributes to pin threads on each processor
#endif
static unsigned int pthread_count = 0;
#define MAX_THREADS 32
pthread_t PThreadTable[MAX_THREADS] __attribute__((aligned(32)));
#define SIM_SOCLIB
#define QEMU_ADDR_BASE                              0x82000000
#define SYSTEMC_SHUTDOWN                            0x0004
#define EXIT_SIM_SUCCESS (*((volatile uint32_t *)(QEMU_ADDR_BASE + SYSTEMC_SHUTDOWN)) = 1)
#define EXIT_SIM_FAILURE (*((volatile uint32_t *)(QEMU_ADDR_BASE + SYSTEMC_SHUTDOWN)) = 0)
')

define(ENV, ` ')
define(EXTERN_ENV, `
#include <pthread.h>
#include <sys/time.h>
#include <unistd.h>
#include <stdlib.h>
#define SIM_SOCLIB
')

define(G_MALLOC, `malloc($1);')
define(G_FREE, `;')
define(G_MALLOC_F, `malloc($1)')
define(NU_MALLOC, `malloc($1);')
define(NU_FREE, `;')
define(NU_MALLOC_F, `malloc($1)')

define(GET_HOME, `{($1) = 0;}')
define(GET_PID, `{($1) = 0;}')
define(AUG_DELAY, `{sleep ($1);}')
define(ST_LOG, `{;}')
define(SET_HOME, `{;}')
define(CLOCK, `{
    struct timeval  FullTime;

    gettimeofday(&FullTime, NULL);
    ($1) = (unsigned long)(FullTime.tv_usec + FullTime.tv_sec * 1000000);
}')


divert(0)
